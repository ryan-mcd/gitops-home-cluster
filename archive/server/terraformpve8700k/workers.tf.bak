resource "proxmox_vm_qemu" "kube-worker" {
  for_each = var.workers

  name        = each.key
  target_node = each.value.target_node
  agent       = 1
  clone       = "${each.value.target_node}-${var.common.clone}"
  #clone       = var.common.clone
  clone_wait = 0
  vmid       = each.value.id
  memory     = each.value.memory
  cores      = each.value.cores
  vga {
    type = "qxl"
  }
  network {
    model    = "virtio"
    macaddr  = each.value.macaddr
    bridge   = "vmbr0"
    tag      = 924
    firewall = true
  }
  disk {
    type    = "scsi"
    storage = "zfs1tbblue"
    size    = each.value.disk
    format  = "raw"
    ssd     = 1
    discard = "on"
  }
  disk {
    type    = "virtio"
    storage = "zfs1tbblue"
    size    = each.value.virtiodisk
    format  = "raw"
    ssd     = 1
    discard = "on"
  }
  serial {
    id   = 0
    type = "socket"
  }
  lifecycle {
    ignore_changes  = [
      network,
    ]
  }
  bootdisk     = "scsi0"
  scsihw       = "virtio-scsi-pci"
  os_type      = var.common.os_type
  ipconfig0    = "ip=${each.value.cidr},gw=${each.value.gw}"
  ciuser       = "ubuntu"
  cipassword   = data.sops_file.secrets.data["data.user_password"]
  searchdomain = data.sops_file.secrets.data["data.search_domain"]
  nameserver   = var.common.nameserver
  sshkeys      = var.ssh_pub_key

  connection {
    type     = "ssh"
    user     = "ubuntu"
    private_key = data.sops_file.secrets.data["data.ssh_key"]
    host     = each.value.ip
  }

  provisioner "remote-exec" {
    inline = [
      "HISTCONTROL=ignorespace",
      " echo \"${data.sops_file.secrets.data["data.user_password"]}\" | sudo -S -k /bin/bash -c '\"%ubuntu ALL=(ALL) NOPASSWD:ALL\" >> /etc/sudoers.d/ubuntu'",
      "ssh-keygen -b 2048 -t rsa -f ~/.ssh/id_rsa -q -N \"\"",
      "echo ${var.addl_pub_key1} >> ~/.ssh/authorized_keys",
      "echo ${var.addl_pub_key2} >> ~/.ssh/authorized_keys",
    ]
  }
}
